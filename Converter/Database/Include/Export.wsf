<package>
<job id="VBExport">
<script language="VBScript" src="Common.vbs" />
<script language="VBScript">
Option Explicit

Dim g_strExportTime

'----------------------------------------------------------------------
' Generates the name of a filelist.
'----------------------------------------------------------------------
Function GetOutputFileName(strOutputFolder, strDomain, strComputerName, nFileNumber)

	GetOutputFileName = strOutputFolder & "\" & "Filelist_" & strDomain & "_" & strComputerName & "_" & g_strExportTime & "_" & Right("00000" & nFileNumber, 6) & ".xml"

End Function

'----------------------------------------------------------------------
' Export for all the database.
'----------------------------------------------------------------------
Sub ExportAll(objDatabae, strOutputFolder, filterId)

    Dim strSQL
    Dim qResults
    Dim strDomain, strComputerName
    Dim columnIndex
    
    strSQL = "SELECT DISTINCT DNS, ComputerName FROM [dbo].[omFile]"
    Set qResults = objDatabase.ExecuteWithResults(strSQL)
    
    If qResults.Rows = 0 Then
        WScript.Echo "No file lists exist. There are no ComputerName, Domain pairs in the database."
        WScript.Quit ErrorNoFileList
    End If
    
    For columnIndex = 1 To qResults.Rows
        strDomain = qResults.GetColumnString(columnIndex,1)
        strComputerName = qResults.GetColumnString(columnIndex,2)
        Export objDatabase, strDomain, strComputerName, strOutputFolder, filterId
    Next

End Sub

'----------------------------------------------------------------------
' Export for a given Domain and ComputerName.
'----------------------------------------------------------------------
Sub Export(objDatabase, strDomain, strComputerName, strOutputFolder, filterId)
    
    Dim qResults
    Dim nLastFileId
    Dim nWriteNumber
    Dim bWritten
    Dim strLastFileId
    Dim strSQL
    Dim strXML
    
    WScript.Echo "Exporting list for Domain:" & strDomain & " Computer:" & strComputerName
    
    strLastFileId = "0"
    nWriteNumber = 1
    bWritten = False

    Do While strLastFileId <> "" 
        strSQL = "EXECUTE [dbo].[Xp_ExportFileList] N'" & strDomain & "', N'" + strComputerName + _
            "', " & strLastFileId
        If (filterId >= 0) Then
            strSQL = strSQL & ", " & filterId
        End If
        
        On Error Resume Next
        Set qResults = objDatabase.ExecuteWithResults(strSQL)
        If (Err.Number <> 0) Then
                WScript.Echo "ERROR: Failed to export file list."
                WScript.Echo Err.Description
                WScript.Quit ErrorExportFailed
                On Error Goto 0
                Exit Sub
        End If
        On Error Goto 0

        On Error Resume Next
        strXML = qResults.GetColumnString(1, 1)
        
        If Err.Number = 0 Then
            bWritten = True
            strLastFileId = WriteXMLtoFile(qResults, GetOutPutFileName(strOutputFolder, strDomain, strComputerName, nWriteNumber))
        Else
            strLastFileId = ""
        End If
        On Error Goto 0
                        
        nWriteNumber = nWriteNumber + 1
    Loop
    
    If not bWritten Then
        WScript.Echo "Empty list. No file list exported for this domain and computer."
    End If
    WScript.Echo
        
End Sub

'----------------------------------------------------------------------
' Write the given string into the given file
'----------------------------------------------------------------------
Function WriteXMLtoFile(queryResults, strFullFilePath)
    
    Dim tfhOutput
    Dim strXml, strXmlReplaced
    Dim msXml
    Dim node
    Dim columnIndex
    
    Set msXml = CreateObject("Microsoft.XMLDOM")
    If (Err.Number <> 0 or msXml Is Nothing) Then
	    WScript.Echo "MSXML is not present or properly installed."
	    WScript.Quit ErrorCreateXMLDOM
    End If
    
    strXml = "<?xml version=""1.0"" encoding=""UTF-8"" ?>"
    strXml = strXml + "<OMPMFiles>"

    For columnIndex = 1 To queryResults.Rows
	    strXml = strXml + queryResults.GetColumnString(columnIndex, 1)
	Next
	strXml = strXml + "</OMPMFiles>"

    strXmlReplaced = replace(strXml, "<Files>", "<File>", 1, -1, 1)
    strXmlReplaced = replace(strXmlReplaced, "</Files>", "</File>", 1, -1, 1)
    strXmlReplaced = replace(strXmlReplaced, "<Issues/>", "")
    strXmlReplaced = replace(strXmlReplaced, "</Issues><Issues>", "")
    
	msXml.loadXML(strXmlReplaced)
   
    msXml.setProperty "SelectionLanguage", "XPath"
    Set node = msXml.selectSingleNode("(//OMPMFiles/File/FileId)[last()]")
    If (Err.Number <> 0 or node Is Nothing) Then
                WScript.Echo "ERROR: XPath query failed."
                WScript.Echo Err.Description
        WriteXMLtoFile = ""
    Else
        WriteXMLtoFile = node.Text
    End If
    
    msXml.save(strFullFilePath)
	
End Function


'----------------------------------------------------------------------
' Main entry point.
'
' Arguments Expected: 
'
'  1) To export all file lists:
'
'		Server Name
'		Database Name
'		Output Folder 
'
'  2) To export a single file list.
'
'		Server Name
'		Database Name
'		Output Folder 
'		Domain Name
'       Computer Name
'----------------------------------------------------------------------

Dim args
Dim strServer
Dim strDatabase
Dim strOutputFolder
Dim strDomain
Dim strComputerName
Dim filterId
filterId = -1

' Make sure we have the right number of arguments.
Set args = WScript.Arguments
If (args.Count <> 3 and args.Count <> 5 and args.Count <> 7) Then
	WScript.Echo "ERROR: Invalid argument."
	WScript.Quit ErrorInvalidArgs
End If

strServer = args(0)
strDatabase = args(1)
strOutputFolder = args(2)

Dim objSQLServer
Dim objDatabase
GetSQLServer objSQLServer 
Connect objSQLServer, strServer
GetDatabase objSQLServer, objDatabase, strDatabase

g_strExportTime = ""  & Year(now()) & "-" & Right("0" & Month(now()), 2) & "-" & _
                        Right("0" & Day(now()), 2) & "-" & Right("0" & Hour(now()), 2) & "-" & _
                        Right("0" & Minute(now()), 2) & "-" & Right("0" & Second(now()), 2)                       

If(args.Count = 5 Or args.Count = 7) Then
    If(args(3) = "/w") Then
        filterId = args(4)
        ExportAll objDatabase, strOutputFolder, filterId
    Else
        strDomain = args(3)
        strComputerName = args(4)
        If(args.Count = 7) Then
            If(args(5) = "/w") Then
                filterId = args(6)
            End If
        End If
        Export objDatabase, strDomain, strComputerName, strOutputFolder, filterId
    End If
    
Else
    ExportAll objDatabase, strOutputFolder, -1
End If

Set objSQLServer = Nothing
Set objDatabase = Nothing
set g_fso = Nothing

</script>
</job>
</package>